// tag::propdeps-plugin-1[]
buildscript {
	ext {
		springBootVersion = '2.0.0.BUILD-SNAPSHOT'
		springCloudVersion = 'Finchley.BUILD-SNAPSHOT'
	}
	repositories {
		mavenCentral()
		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://repo.spring.io/milestone" }
		maven { url "http://repo.spring.io/plugins-release" }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("org.springframework.build.gradle:propdeps-plugin:0.0.7")
	}
}
// end::propdeps-plugin-1[]

plugins {
	id "io.spring.dependency-management" version "1.0.0.RELEASE"

	// tag::gradle-git[]
	id "com.gorylenko.gradle-git-properties" version "1.4.17"
	// end::gradle-git[]
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'

// tag::propdeps-plugin-2[]
apply plugin: 'propdeps'
apply plugin: 'propdeps-maven'
apply plugin: 'propdeps-idea'
apply plugin: 'propdeps-eclipse'
// end::propdeps-plugin-2[]

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
	maven { url "https://repo.spring.io/snapshot" }
	maven { url "https://repo.spring.io/milestone" }
	maven { url "https://repo.spring.io/release" }
	maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
	maven { url "https://dl.bintray.com/rabbitmq/maven-milestones/" }
}

jar {
	baseName = 'learning-spring-boot-chat'
	version = '0.0.1-SNAPSHOT'
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-webflux')
	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile("org.springframework.boot:spring-boot-starter-thymeleaf")
	compile('org.springframework.boot:spring-boot-devtools')
	compile('org.projectlombok:lombok')
	compile('org.springframework.cloud:spring-cloud-starter-stream-rabbit')
	compile('org.springframework.cloud:spring-cloud-starter-eureka')
	compile('org.springframework.cloud:spring-cloud-starter-hystrix')
	compile('org.springframework.cloud:spring-cloud-starter-config')
	compile('org.springframework.boot:spring-boot-starter-websocket')

	compile('org.webjars:requirejs:2.2.0')
	compile('org.webjars.npm:stompjs:2.3.3') {
		exclude module: 'websocket' // We are using SockJS for websocket functionality
	}
	compile('org.webjars.bower:sockjs-client:1.1.0')

	compile('org.springframework.boot:spring-boot-starter-security')

	//tag::websocket[]
	compile('org.springframework.security:spring-security-messaging')
	//end::websocket[]

	compile('org.springframework.session:spring-session-data-mongo')

	// tag::zuul[]
	compile('org.springframework.cloud:spring-cloud-starter-zuul')
	// end::zuul[]

	// tag::thymeleaf-security[]
	compile('org.thymeleaf.extras:thymeleaf-extras-springsecurity4:2.1.3.RELEASE')
	// end::thymeleaf-security[]

	// tag::configuration-processor[]
	optional "org.springframework.boot:spring-boot-configuration-processor"
	// end::configuration-processor[]

	testCompile('org.springframework.boot:spring-boot-starter-test')
}

// tag::configuration-processor-2[]
compileJava.dependsOn(processResources)
// end::configuration-processor-2[]

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}
